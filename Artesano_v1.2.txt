; ======================================================================================================
;  '||   ||` '||`   ||                                 /.\      '||`
;   ||   ||   ||    ||     ''                         // \\      ||   ''
;   ||   ||   ||  ''||''   ||  '||),,(|,   '''|.     //...\\     ||   ||   '''|.  `||''|,  '''/  '''|.
;   ||   ||   ||    ||     ||   || || ||  .|''||    //     \\    ||   ||  .|''||   ||  ||   //  .|''||
;   `|...|'  .||.   `|..' .||. .||    ||. `|..||. .//       \\. .||. .||. `|..||. .||  ||. /... `|..||.
; ======================================================================================================
;
; Script Name: Artesania
;   ______________________
; .'   __                 `.
; |  .'__`.    = = = =     |_.-----._                          .---.
; |  `.__.'    = = = =     | |     | \ _______________        / .-. \
; |`.                      | |     |  |  ````````````,)       \ `-' /
; |  `.                    |_|     |_/~~~~~~~~~~~~~~~'         `---'
; |    `-;___              | `-----'                   ___
; |        /\``---..._____.'               _  _...--'''   ``-._
; |       |  \                            /\\`                 `._
; |       |   )              __..--''\___/  \\     _.-'```''-._   `;
; |       |  /              /       .'       \\_.-'            ````
; |       | /              |_.-.___|  .-.   .'~
; |       `(               | `-'      `-'  ;`._
; |         `.              \__       ___.'  //`-._          _,,,
; |           )                ``--../   \  //     `-.,,,..-'    `;
; `----------'                            \//,_               _.-'
;                                          ^   ```--...___..-'
; Author: Sthil Lauda
; Version: 1.2
; Client Tested with: 2.0.3
; EUO version tested with: EasyUO 1.5 Version 243
; Shard: Ultima Alianza (¡Hasta la muerte!)
; Revision Date: 18/08/2019
; Public Release:  05/08/2019
; Purpose: Subir la skill de Artesanía fabricando resortes y fundiéndolos para recuperar mineral :-)
;________________________________
;
; ///////////////////////////////
; ******** CONFIGURACIÓN ********
; ///////////////////////////////
;________________________________
;
set %cofre UXVXMND                ; Donde tienes lingotes.
set %bolsa_herramientas UXVXMND   ; Bolsa con Herramientas de artesano.
set %bolsa_mazas UXVXMND          ; Bolsa con Mazas de Herrero.
set %tiempo_alimentacion 360
;________________________________________________
;
; //////////////////////////////////////////////
; ************* FIN CONFIGURACIÓN *************
; //////////////////////////////////////////////
;________________________________________________
;
set %resortes TCG
set %herramientas GTL
set %martillos GBG_FBG_TLH_CGG
set %hierro RMK
set %comida RGG_NRD_QSD_ZBG_WLI_QRD_END_FUD_YLI_WLI_HGI
 +_KGI_EGI_QLI_KLI_MLI_KRD_HQD_AQD_MQE_VQE_TQE_JQE_IQE
 +_GQE_SPE_ZPE_OQE_BDF_KPE_RQE_YSD_SQD_YWI_AXI_PQD_QQD
set %tiempo_actual #SCNT
set %proxima_vez #SCNT
set %_worldsavejournalindex #jindex ; Journal para escanear en busca de save's del server.
set *CEOWorldSaving #FALSE          ; Indica si hay un save del server en marcha
gosub wait_for GUMP 530_480 CLOSE

;========================================================
;
; ******************* LOOP PRINCIPAL ********************
;
;========================================================
inicio:
gosub CEOWorldSaveChecker Dentro_de_1_minuto grabacion_del_mundo bases_de_datos 45 120
gosub checkHungry
gosub hierro
gosub herramientas
gosub martillos
gosub resortes
goto inicio
;========================================================
;
; ***************** FIN LOOP PRINCIPAL *****************
;
;========================================================

;*************************************************
;                  ¿COMER?
;*************************************************
sub checkHungry
set %tiempo_actual #SCNT
if %proxima_vez <= %tiempo_actual
   {
     gosub comprobar_comida
   }
return

;*************************************************
;                  REPONER HIERRO
;*************************************************
sub hierro
finditem %hierro C_ , #BACKPACKID
if #FINDSTACK <= 3
{
  finditem %hierro C_ , %cofre
  if #FINDSTACK >= 50
     {
       exevent drag #FINDID 50
       exevent dropc #BACKPACKID
       wait 30
     }
     else
        {
          set #SYSMSGCOL 38
          event sysmessage NO QUEDAN LINGOTES! :-(
          halt
        }
}
return

;*************************************************
;               COGER HERRAMIENTAS
;*************************************************
sub herramientas
finditem %herramientas C_ , #BACKPACKID
if #FINDCNT = 0
{
  _herramienta:
  finditem %herramientas C_ , #BACKPACKID
  if #FINDCNT < 2
     {
       finditem %herramientas C_ , %bolsa_herramientas
       if #FINDCNT > 2
          {
            exevent drag #FINDID 1
            exevent dropc #BACKPACKID
            wait 30
            goto _herramienta
          }
          else
             {
               set #SYSMSGCOL 38
               event sysmessage NO QUEDAN HERRAMIENTAS! :-(
               halt
             }
     }
}
return

;*************************************************
;               COGER MARTILLOS
;*************************************************
sub martillos
finditem %martillos C_ , #CHARID
if #FINDCNT = 0
{
  _martillo:
  finditem %martillos C_ , #BACKPACKID
  if #FINDCNT < 2
     {
       finditem %martillos C_ , %bolsa_mazas
       if #FINDCNT >= 2
          {
            exevent drag #FINDID 1
            exevent dropc #BACKPACKID
            wait 30
            goto _martillo
          }
          else
             {
               set #SYSMSGCOL 38
               event sysmessage NO QUEDAN MAZAS! :-(
               halt
             }
     }
  finditem %martillos C_ , #BACKPACKID
  if #FINDCNT > 0
     {
       set #LOBJECTID #FINDID
       event macro 17
       wait 20
     }
}
return

;*************************************************
;                HACER RESORTES
;*************************************************
sub resortes
finditem %herramientas C_ , #BACKPACKID
if #FINDCNT > 0
   {
     set #LOBJECTID #FINDID
     event macro 17
     gosub wait_for GUMP 530_480 50
     contpos 40 40
     click 70 150 f dmc
     gosub wait_for GUMP 530_480 50
     contpos 40 40
     click 270 230 f dmc
     gosub wait_for GUMP 530_480 100
   }
finditem %resortes C_ , #BACKPACKID
if #FINDSTACK > 0
{
  finditem %hierro C_ , #BACKPACKID
  if #FINDCNT > 0
     {
       if #FINDSTACK > 0
          {
            set #LOBJECTID #FINDID
            event macro 17
            wait 10
            contpos 40 40
            click 69 423 f dmc
            target
            finditem %resortes C_ , #BACKPACKID
            if #FINDCNT > 0
               {
                 for #FINDINDEX 1 #FINDCNT
                     {
                       target
                       set #LTARGETID #FINDID
                       set #LTARGETKIND 1
                       event macro 22
                       wait 10
                     }
               }
          }
     }
  key ESC
}
goto inicio

;=====================================================
; Rutina para, en caso de hambre, comer hasta saciarse
;=====================================================
; v2.2
sub comprobar_comida
{
  hambriento:
  set %jrnl #jindex ; Marcamos un índice de las líneas del diario.
  event macro 4 0 .hungry
  wait 20
  while #true
     {
       if #jindex > %jrnl ; Si aparece una nueva línea.
          {
            set %jrnl %jrnl + 1  ; Seleccionamos ese numero de línea.
            scanjournal %jrnl   ; La leemos...
            if Dentro_de_1_minuto in #journal
               {
                 wait 120s ; Save del server...
               }
            if Estas_no_le_afecta_el_hambre in #journal || Estas_tan_lleno_que_no_puedes in #journal || Estas_totalmente_saciado in #journal
               {
                 set %proxima_vez %tiempo_actual + %tiempo_alimentacion
                 return
               }
               else
                  {
                    if Estas_lleno in #journal || Estas_contento in #journal || Estas_en_inanicion in #journal || Estas_hambriento in #journal || Sientes_mucho in #journal || Estas_con_hambre in #journal || Estas_parcialmente in #journal || Estas_bien_lleno in #journal || Te_sientes_casi_lleno in #journal
                       {
                         finditem %comida C_ , #backpackid
                         if #findcnt > 0
                            {
                              set #lobjectid #findid
                              event macro 17
                              wait 20
                              goto hambriento
                            }
                            else
                               {
                                 event exmsg #charid 3 48 COMPRA COMIDA!!
                                 wait 40
                                 set %proxima_vez %tiempo_actual + %tiempo_alimentacion
                               }
                       }
                       else ; Mensaje extraño, repetimos...
                          {
                            goto hambriento
                          }
                  }
          }
       return
     } ; FIN While
}
return

;-----------------------------------------------------------
; Script Name: CEOWorldSaveChecker
; Author: CEO
; Version: 1.0
; Client Tested with: 4.0.10b
; EUO version tested with: 1.42.00A5
; Shard OSI / FS: OSI
; Revision Date: 060105
; Public Release: 060105
; Global Variables Used:  *CEOWorldSaving
; Purpose: Used to catch the world save message for freeshards and temporarily pause mainscript during save.
;-------------------------------------------------------------
sub CEOWorldSaveChecker
; sub to pause a script during a RunUO world save.
set %_text1 %1
set %_text2 %2
set %_text3 %3
if %_worldsaveupcoming
{
        if %_worldsavetimer < #scnt
        {
                set %_worldsavetimer #scnt + %5
                gosub popupworldsave 25 15 blue
                goto _WSC_WAIT1
        }
        return
}
_WSC1:
if %_worldsavejournalindex > #jindex
        return
scanjournal %_worldsavejournalindex
if %_text1  in #JOURNAL
{
        set %_worldsaveupcoming #true
        set %_worldsavetimer #scnt + %4
        set %_worldsavejournalindex  %_worldsavejournalindex + 1
        return
}
set %_worldsavejournalindex  %_worldsavejournalindex + 1
goto _WSC1
_WSC_WAIT1:
if %_worldsavetimer < #scnt
        goto _WSC_Complete
wait 5
set *CEOWorldSaving #true
if %_worldsavejournalindex > #jindex
        goto _WSC_WAIT1
scanjournal %_worldsavejournalindex
if %_text2 in #journal
{
        gosub popupworldsave 25 15 yellow
        set %_worldsavejournalindex  %_worldsavejournalindex + 1
        goto _WSC_WAIT2
}
set %_worldsavejournalindex  %_worldsavejournalindex + 1
goto _WSC_WAIT1
_WSC_WAIT2:
if %_worldsavetimer < #scnt
        goto _WSC_Complete
wait 5
if %_worldsavejournalindex > #jindex
        goto _WSC_WAIT2
scanjournal %_worldsavejournalindex
if %_text3 notin #journal
{
        set %_worldsavejournalindex  %_worldsavejournalindex + 1
        goto _WSC_WAIT2
}
_WSC_Complete:
set %_worldsavejournalindex  #jindex
set %_worldsaveupcoming #false
set *CEOWorldSaving #false
menu delete worldsave
return

sub popupworldsave
set %x %1
set %y %2
set %worldcolor %3
menu Clear
menu Window Title World Save Monitor
menu Window Color Black
menu Window Size 160 160
menu shape worldsave 20 15 125 125 1 7 2 olive 7 %worldcolor
menu font bgcolor %worldcolor
menu font color lime
menu font type b
menu font size 15
set %x %x + 30
set %y %y + 30
menu text worldsave %x %y World
set %x %x + 15
set %y %y + 20
menu text worldsave %x %y Save
set %x %x + 15
set %y %y + 20
menu text worldsave %x %y Wait
menu font size 9
menu font bgcolor black
menu font color lime
menu font type n
menu Show 920 770
return

;=================================================================
sub wait_for
;=================================================================
; This "wait_for" sub package was created by Locke. If you use these subs please keep this header intact.
; Documentation: the sub package grew to large with all the comments. To load them call the sub like this:
; gosub wait_for docs    | or you can use doc, documention, what, or my personal favorite, kickass.

; "wait_for core dispatcher" version 1.2 by Locke
if %1 = doc || if %1 = docs || if %1 = documentation || if %1 = what || if %1 = kickass
{
Display ok Please click ok and wait for your browser to start.
execute http://www.easyuo.com/forum/viewtopic.php?t=24716
halt
}
nameSpace Push
namespace local LLNS
set #result N/A ; if #result isn't set by one of my wait_for subs it'll throw an error.
set !LPC #lpc
set #lpc 200
for %i 0 %0
set !_A . %i % . %i
gosub wait_for_ , !_A1
set #lpc !lpc
namespace clear LLNS
namespace pop
if #result <> N/A
return #result
else
display ok You specified an unknown wait_for command. Script returned #result and is halting.
halt
;=================================================================
sub wait_for_MSG ; version 1.4 ~Locke
;=================================================================
if !_A0 < 3
{
display ok You haven't specified enough vars.$
+The basic format is: gosub wait_for MSG %message time_out_in_seconds$
+Script is halting
halt
}
set !_timeout ( #scnt2 + !_A . !_A0 )
_lets_wait_for_a_message:
for %i %jstart #jindex
{
 scanjournal %i
  for %ii 2 !_A0
  {
    if !_A . %ii in #journal && %jstart <> %i
    set #result #true , #spc , !_A . %ii
  }
}

if #true notin #result
{
set #result #false
 if !_timeout =< #scnt2
 return #result
wait 1
goto _lets_wait_for_a_message
}
else
return #result

wait 1
goto _lets_wait_for_a_message

;======================================================================
sub wait_for_GUMP ; version 1.4 ~Locke
;=================================================================
; Añadido #CONTID ^^
if !_A0 < 3
{
display ok You haven't specified enough vars.$
+The basic format is: gosub wait_for GUMP XXX_YYY time_out_in_seconds$
+Script is halting
halt
}
if !_A3 <> CLOSE
set !_timeout #scnt2 + !_A3
else
set !_timeout 0 ; no gump wait if %3 = CLOSE since we already believe it to be open.

_lets_wait_for_a_gump:
if #CONTSIZE = !_A2 || #CONTKIND = !_A2 || #CONTNAME = !_A2 || #CONTTYPE = !_A2 || #CONTID = !_A2
{
 if !_A5 <> N/A && !_A6 <> N/A
 {
 set !clickx !_A5 + #contposx
 set !clicky !_A6 + #contposy
 click !clickx !clicky
 }
 if !_A3 = CLOSE || if !_A4 = CLOSE || !_A5 = CLOSE || !_A6 = CLOSE
 {
 set !string #contsize
 str pos !string _
 set !pos #strres
 str del !string #strres #strres
 set !clickX #contposx + ( #strres / 2 )
 str del !string 1 !pos
 set !clickY #contposy + ( #strres / 2 )
 click !clickx !clicky R
 }
return #true
}

if ( !_timeout =< #scnt2 ) && ( !_A4 = N/A )
   {
     return #false
   }

if !_A4 <> N/A && !_timeout =< #scnt2
{
finditem !_A4
if #findkind <> -1
{
 for #findindex 1 #findcnt
 {
  if #finddist > 2
  ignoreitem #findid LLWAITFOR
  else
  break
 }
 set #lobjectid #findid
 event macro 17 0
 set !_timeout #scnt2 + !_A3
}
else
return #false
}
wait 1
goto _lets_wait_for_a_gump

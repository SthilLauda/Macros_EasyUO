; ======================================================================================================
;  '||   ||` '||`   ||                                 /.\      '||`
;   ||   ||   ||    ||     ''                         // \\      ||   ''
;   ||   ||   ||  ''||''   ||  '||),,(|,   '''|.     //...\\     ||   ||   '''|.  `||''|,  '''/  '''|.
;   ||   ||   ||    ||     ||   || || ||  .|''||    //     \\    ||   ||  .|''||   ||  ||   //  .|''||
;   `|...|'  .||.   `|..' .||. .||    ||. `|..||. .//       \\. .||. .||. `|..||. .||  ||. /... `|..||.
; ======================================================================================================
;
; Script Name: Entrenar Familiar
;                    __                  __
;                   ( _)                ( _)
;                  / / \\              / /\_\_
;                 / /   \\            / / | \ \
;                / /     \\          / /  |\ \ \
;               /  /   ,  \ ,       / /   /|  \ \
;              /  /    |\_ /|      / /   / \   \_\
;             /  /  |\/ _ '_| \   / /   /   \    \\
;            |  /   |/  0 \0\    / |    |    \    \\
;            |    |\|      \_\_ /  /    |     \    \\
;            |  | |/    \.\ o\o)  /      \     |    \\
;            \    |     /\\`v-v  /        |    |     \\
;             | \/    /_| \\_|  /         |    | \    \\
;             | |    /__/_     /   _____  |    |  \    \\
;             \|    [__]  \_/  |_________  \   |   \    ()
;              /    [___] (    \         \  |\ |   |   //
;             |    [___]                  |\| \|   /  |/
;            /|    [____]                  \  |/\ / / ||
;       snd (  \   [____ /     ) _\      \  \    \| | ||
;            \  \  [_____|    / /     __/    \   / / //
;            |   \ [_____/   / /        \    |   \/ //
;            |   /  '----|   /=\____   _/    |   / //
;         __ /  /        |  /   ___/  _/\    \  | ||
;        (/-(/-\)       /   \  (/\/\)/  |    /  | /
;                      (/\/\)           /   /   //
;                             _________/   /    /
;                            \____________/    (
;
; Author: SthiL
; Version: 1.0
; Client Tested with: 2.0.3
; EUO version tested with: EasyUO 1.5 Version 243
; Shard: Ultima Alianza (¡Hasta la muerte!)
; Public Release: 28/03/2019
; Global Variables Used: none
; Purpose: Subir de nivel al pobre familiar :(

;________________________________
;
; ///////////////////////////////
; ******** CONFIGURACIÓN ********
; ///////////////////////////////
;________________________________
;
set %tiempo_espera 10    ; Cada cuantos segundos atacar.
set %tiempo_curacion 300 ; Cada cuantos segundos curar al objetivo.
;________________________________________________
;
; //////////////////////////////////////////////
; ************* FIN CONFIGURACIÓN *************
; //////////////////////////////////////////////
;________________________________________________
;
set %siguiente_kill #SCNT
set %siguiente_invasmani #SCNT + %tiempo_curacion ; 5 Minutos

gosub setupHeal
gosub setupObjetivo

loop:
findItem %objetivo G_15
if #findkind = -1 ; No hay objetivo...
   {
     wait 1s ; Esperamos...
     goto loop
   }
   else ; Hay un objetivo...
      {        
        if ( #SCNT >= %siguiente_kill )
             {
               gosub allKill
             }
        if ( %curar )
             {
               if ( #SCNT >= %siguiente_invasmani )
                    {
                      gosub inVasMani
                    }
             }
      }
goto loop

sub setupHeal
display yesno ¿Quieres curar al objetivo usando la magia? (In Vas Mani)
if #DISPRES = Yes
   {
     set %curar #TRUE
   }
   else
      {
        set %curar #FALSE
      }
return

sub setupObjetivo
set #SYSMSGCOL 73
event sysmessage CLICK EN EL OBJETIVO
wait 20
set #TARGCURS 1
while #TARGCURS = 1
      {
        wait 1
      }
set %objetivo #LTARGETID
gosub GetName %objetivo nombre_objetivo
set #SYSMSGCOL 88
event sysmessage OBJETIVO: %nombre_objetivo
wait 20
return

sub allKill
event macro 1 0 All Kill
target
set #LTARGETID %objetivo
set #LTARGETKIND 1
event macro 22
wait 20
set %siguiente_kill %siguiente_kill + %tiempo_espera
return

sub inVasMani
event macro 15 28 ; Mayor Curación (In Vas Mani).
target 2s
set #LTARGETID %objetivo
set #LTARGETKIND 1
event macro 22
wait 3s
set %siguiente_invasmani #SCNT + %tiempo_curacion
return

;* @name GetName
;* @author Roadkill
;* @ver 1.0 24Jan04
;* @purpose get the name of a creature/vendor
;* @params %1= the id of the thing whose name you want, required
;*      @%2= variable name to return the ID in, required
;* @returns @%2 and #result
;* @changes #property, #strres, @%2, !strlength
;* @example call rksubs.txt GetName %beetle beetlename
;* @status: tested
sub GetName
        set %id_property %1
        gosub waitforprop %id_property
        str pos #property $
        set !strlength #strres - 1
        str left #property !strlength
        set % . %2 #strres
return % . %2

sub waitforprop ; <===================================================>
event property #charid
wait 1
set %oldprop #property
event property %1
_testforprop:
if #property = %oldprop
   {
     goto _testforprop
   }
return
